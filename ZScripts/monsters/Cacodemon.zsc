class fCacodemon : Cacodemon replaces Cacodemon
{
Default
	{
	species "Cacodemon";
	Health 400;
	Radius 31;
	Height 56;
	Mass 400;
	Speed 8;
	PainChance 128;
	Monster;
	+FLOAT +NOGRAVITY
	SeeSound "caco/sight";
	PainSound "caco/pain";
	DeathSound "caco/death";
	ActiveSound "caco/active";
	Obituary "$OB_CACO";
	HitObituary "$OB_CACOHIT";
	Tag "$FN_CACO";
	+FORCEXYBILLBOARD
	bloodcolor "00 00 CC";
	gibhealth 50;
	+FLOATBOB
	FloatBobStrength 0.35;
	BDoomMonster.smokeheight 30;
	}
override void Tick() {
	super.Tick();
	if (!InStateSequence(curstate,FindState("Spawn")))
		bFLOATBOB = false;
	}
states
	{
	Spawn:
		HEA1 AAAAA 10 A_Look();
		HEA1 B 5 A_Look();
		HEA1 C 5;
		HEA1 B 5 A_Look();
		HEA1 A 5;
		loop;
	See:
		HEA1 AAAAAAAAAA 3 A_Chase();
		HEA1 BCBA 3 A_Chase();
		loop;
	Missile:
		HEAA ABCCDD 1 A_FaceTarget();
		HEAA E 4 A_CustomComboAttack("BD_CacodemonBall", 32, 10 * random(1, 6));
		HEAA CCA 1;
		Goto See;
	Pain:
		HEAA F 3;
		HEAA G 3 A_Pain();
		HEAA HGF 2;
		Goto See;
	Crush:
		CRS2 B -1;
		stop;
	Death:
		TNT1 A 0 {
			scale.x*=randompick(-1,1);
			bFORCEXYBILLBOARD = false;
			if (burning)
				return ResolveState("Death.Vanilla");
			return A_jump(256,"Death.Vanilla","Death1", "Death2");
			return ResolveState(null);	
			}
	Death.Vanilla:	
		HEAM A 3 BD_Bleed();
		HEAM B 3 A_Scream();
		HEAM CD 3 BD_Bleed();
		HEAM E 3 A_NoBlocking();
		HEAM F 3 A_SetFloorClip();
		HEAM GHI 3;
		goto deathbleed;
	Death1: //no eye, intestines fall down
		HEAN AB 3;
		TNT1 A 0 A_Scream();
		HEAN C 3 {
			if (bdoom_gibs && !burning)
				A_SpawnItemEx("CacodemonEye",frandom(-2,2),frandom(-2,2),frandom(28,32),frandom(2,5),0,frandom(0,5),frandom(-10,10),SXF_SETTARGET);
			}
		 TNT1 A 0 BD_Bleed();
		HEAN DE 3;
		HEAN F 3 A_NoBlocking();
		 TNT1 A 0 BD_Bleed();
		HEAN GHIJKL 3;
		TNT1 A 0 A_SetFloorClip();
		HEAN MNO 5;
		goto deathbleed;
	Death2: //puke gibs, less common death
		TNT1 A 0 {
			if (!bdoom_gibs || random(1,3) == 3)
				return ResolveState ("Death");
			bNOGRAVITY = true; 
			return ResolveState (null);
			}
		HEAP ABC 4;
		TNT1 A 0 {
			if (bdoom_gibs)
				A_SpawnItemEx("CacodemonEye",frandom(-2,2),frandom(-2,2),frandom(28,32),frandom(2,5),0,frandom(0,5),frandom(-10,10),SXF_SETTARGET);
			}
		HEAP CCDDEEFF 2 {
			if (bdoom_gibs)
				A_SpawnItemEx("CacodemonMeatPiece",0,frandom(-6,6),frandom(8,20),	frandom(3,5),0,frandom(2,3),	random(-15,15),	SXF_SETTARGET,48);
			}
		TNT1 A 0 {
			A_NoBlocking();
			bNOGRAVITY = false;
			}
		HEAP GJ 4;
		HEAP I 4 A_SetFloorClip();
		HEAP JKL 4;
		goto deathbleed;
	Death.Saw: //sliced diagonally
		TNT1 A 0 A_Jump(128,"Death");
		HEAM A 3 BD_Bleed();
		HEAM B 3 A_Scream();
		TNT1 A 0 A_FaceTarget();
		HEAO AABBCCDD 2 {
			if (bdoom_blood)
				A_SpawnItemEx("BloodSpurt",	1,0,frandom(30,38),0,0,0,random(-40,40),SXF_SETTARGET,96);
			}
		TNT1 A 0 {
			A_NoBlocking();
			A_SetFloorClip();
			}
		HEAO EFG 4;
		goto deathbleed;
	xdeath:
		CACX ab 2;
		CACX c 2;
		TNT1 a 0 a_playsound ("misc/gibbed")
		CACX d 2 A_Scream();
		CACX ef 2;
		CACX g 2 A_NoBlocking();
		CACX h 2 a_setfloorclip();
		CACX ijklmnopqr 2;
		CACX s -1;
		stop;
	}
}

Class CacoRedTrail : Actor
{
Default {
	+NOINTERACTION
	+NOBLOCKMAP
	renderstyle "Add";
	alpha 0.8;
	scale 0.05;
	}
states
	{
	Spawn:
		LENR A 1 {
			A_FadeOut(0.2);
			scale*=0.95;
			}
		loop;
	}
}

Class CacoBlueTrail : Flare_General
{
Default {
	renderstyle "Add";
	alpha 0.8;
	scale 0.1;
	}
states
	{
	Spawn:
		LENB A 1 {
			A_FadeOut(0.2);
			scale*=0.95;
			}
		loop;
	}
}

Class BD_CacodemonBall : CacodemonBall //replaces CacodemonBall
{
int broll;
Default {
	+FORCEXYBILLBOARD
	+ROLLSPRITE
	-ROLLCENTER
	-ZDOOMTRANS
	decal "Scorch";
	renderstyle "Add";
	alpha 1.0;
	scale 0.17;
	//seesound "";
	}
override void PostBeginPlay () {
	super.PostBeginPlay();
	scale.x*= randompick(-1,1);
	scale.y*= randompick(-1,1);
	roll = random(0,360);
	broll = random(6,8)*randompick(1,-1);
	A_PlaySound("electric/hum",6,1.0,true,4);
	if (bdoom_debris) {
		let f = BD_ProjFlare(Spawn("BD_ProjFlare",pos));
		f.fcolor = 'blue';
		f.scale = (0.21,0.21);
		f.alpha = 0.3;
		f.master = self;
		
		let lt = CacoBallLightning(Spawn("CacoBallLightning",pos));
		if (!lt) return;
		lt.master = self;
		
		let lt1 = CacoBallLightning(Spawn("CacoBallLightning",pos));
		if (!lt1) return;
		lt1.master = self;
		lt1.upward = true;
		
		}
	}	
override void Tick () {
    Vector3 oldPos = self.pos;    
    Super.Tick();
	if (!bdoom_debris || level.isFrozen() )
		return;
	Vector3 path = level.vec3Diff( self.pos, oldPos );
	double distance = path.length() / 3; //this determines how far apart the particles are
	Vector3 direction = path / distance;
	int steps = int( distance );
	
	for( int i = 0; i < steps; i++ )  {
		Spawn("CacoRedTrail", oldPos );
		Spawn("CacoBlueTrail", oldPos );
		oldPos = level.vec3Offset( oldPos, direction );
		}
	}
States
	{
	Spawn:
		ELBA ABCDEFGHIJKLMNO 2 bright NoDelay {
			roll+=broll;
			}
		loop;
	Death:
		TNT1 a 0 {
			A_StopSound(6);
			A_RemoveChildren(true,RMVF_EVERYTHING );
			if (bdoom_debris) {
				for (int i = 8; i > 0; i--)
					A_SpawnItemEx("CacoBallPiece",frandom(-6,6),frandom(-6,6),frandom(-6,6),frandom(-2,2),frandom(-2,2),frandom(2,4),failchance:16);
				//for (int i = 12; i > 0; i--)
					//A_CustomRailGun(0,0,"","0",RGF_SILENT|RGF_NOPIERCING|RGF_CENTERZ,0,4,"NullPuff",random(0,360),random(0,360),range:24,sparsity:3.0,spawnclass:"CacoballRay");
				let f = BD_BaseFlare(Spawn("BD_BaseFlare",pos));
				f.fcolor = 'blue';
				f.scale = (0.24,0.24);
				f.alpha = 0.8;
				tracer = f;
				}
			}
		ELBX ABCDEFGHIJK 2 bright {
			A_FadeOut(0.01);
			if (tracer) {
				tracer.scale*=0.9;
				tracer.A_FadeOut(0.1);
				}
			roll+=broll;
			}
		TNT1 A 0 {
			if(tracer)
				tracer.destroy();
			}
		stop;
	}
}

Class CacoballRay : Flare_General
{
Default {
	alpha 0.8;
	scale 0.02;
	}
states
	{
	Spawn:
		PEXP ABCDEF 1 bright A_FadeOut(0.0025);
		PEXP GHIJKLM 2 bright A_FadeOut(0.0025);
		stop;
	}
}

Class CacoBallLightning : Flare_General
{
bool upward;
double ceilz;
double flrz;
Default {
	+BRIGHT
	renderstyle "Add";
	scale 0.12;
	alpha 0.0;
	}
override void Tick() {
	super.Tick();
	if (level.IsFrozen())
		return;
	if (!master) {
		destroy();
		return;
		}
	/*if (alpha > 0)
		A_PlaySound("electric/hum",6,1.0,true,1);
	else
		A_StopSound(6);*/
	ceilz = master.ceilingz;
	flrz = master.floorz;
	if (upward) {
		scale.y = (pos.z - ceilz) * 0.00375;
		if (ceilz < pos.z || ceilz - pos.z > 72 || master.ceilingpic == skyflatnum || random(0,15) > 1)
			alpha = Clamp(alpha-0.025,0,0.5);
		else {
			alpha = Clamp(alpha+0.1,0,0.5);
			A_PlaySound("spark",volume:0.8,attenuation:2);
			let piece = CacoBallPiece(Spawn("CacoBallPiece",(pos.x+frandom(-3,3),pos.y+frandom(-3,3),ceilz-0.5)));
			piece.vel = (frandom(-3,3),frandom(-3,3),0);
			}
		}
	else {
		scale.y = (pos.z - flrz) * 0.00375;
		if (flrz > pos.z || pos.z - flrz > 44 || master.floorpic == skyflatnum || random(0,15) > 1)
			alpha = Clamp(alpha-0.025,0,0.5);
		else {
			alpha = Clamp(alpha+0.1,0,0.5);
			A_PlaySound("spark",volume:0.8,attenuation:2);
			let piece = CacoBallPiece(Spawn("CacoBallPiece",(pos.x+frandom(-3,3),pos.y+frandom(-3,3),flrz+0.5)));
			piece.vel = (frandom(-2,2),frandom(-2,2),frandom(2,4));
			}
		}
	}
states
	{
	Spawn:
		TNT1 A 0 NoDelay {
			if (master) {
				SetOrigin(master.pos,false);
				vel = master.vel;
				}
			}
	Idle:
		CLTN ABCDEFGH 2;
		loop;
	}
}

Class CacoBallPiece : RicochetSpark
{
Default {
	alpha 1.0;
	scale 0.08;
	}
states
	{
	Spawn:
		SPRK C 1 bright	{
			A_FadeOut(0.03);
			scale *= 0.92;
			}
		wait;
	}
}
