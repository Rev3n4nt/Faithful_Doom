
// Puffs


//fake bullet that ricochets from bulletpuffs
class RicochetBullet : DebrisGeneral
{
Default {
	+NOINTERACTION
	//+THRUACTORS
	//+DONTSPLASH
	//bouncetype "Hexen";
	speed 40;
	seesound "none";
	deathsound "none";
	renderstyle "Add";
	alpha 0.8;
	}
states
	{
	Spawn:
		TNT1 A 0 NoDelay {
			//if (bdoom_sparks == 1)
				//A_PlaySound("weapons/ricochet");
			}
		PUFF A 2;
		stop;
	}
}

class BdoomPuff : Actor abstract
{
Default {
	+NOBLOCKMAP
	+NOGRAVITY
	+FORCEXYBILLBOARD
	-ALLOWPARTICLES
	+DONTSPLASH
	-FLOORCLIP
	}
}

class EnBulletPuff : BDoomPuff
{
//int hitangle;
string hittex;
string dtype;
int rchance;
bool debris;
int  smoke; //0 - none, 1 - directional, 2 - radial along the wall
int sparkchance;
string decaltype;
Default {
	+NOINTERACTION
	+PUFFGETSOWNER
	decal "none";
	renderstyle "Add";
	scale 0.075;
	alpha 0.8;
	mass 1;
	height 1;
	radius 1;
	}
states
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_SpawnItemEx ("RicoChet",0,0,-5,0,0,0,0,SXF_NOCHECKPOSITION,0);
		TNT1 AAAA 0;

		// TNT1 A 0 NoDelay {
		TNT1 A 0 {
			if (target)
				angle = target.angle;
			int tracepitch;
			int traceheight;
			if (pos.z <= floorz+8) {
				tracepitch = 90;
				traceheight = floorz+8;
				}
			else if (pos.z >= ceilingz-8) {
				tracepitch = -90;
				traceheight = ceilingz-8;
				}
			else {
				tracepitch = 0;
				traceheight = pos.z;
				}
			FLineTraceData puffdata;
			LineTrace(angle,10,tracepitch,TRF_THRUACTORS|TRF_NOSKY|TRF_ABSPOSITION,traceheight,pos.x,pos.y,puffdata);	
			if (puffdata.HitType == TRACE_HitFloor)
				hittex = texman.GetName(floorpic);
			else if (puffdata.HitType == TRACE_HitCeiling)
				hittex = texman.GetName(ceilingpic);
			else if (puffdata.HitType == TRACE_HitWall) {
				hittex = texman.GetName(puffdata.HitTexture);
				}
			
			if (hittex.IndexOf("WATER") >= 0 || hittex.IndexOf("FALL") >= 0 || hittex.IndexOf("BLOOD") >= 0) {
				A_PlaySound("world/watersplashsmall",0,1.0,0,3);
				self.destroy();
				}				
			else if (hittex.IndexOf("LAVA") >= 0 || hittex.IndexOf("FIRE") >= 0 || hittex.IndexOf("NUKAGE") >= 0) {
				A_PlaySound("world/lavasizzlesmall",0,1.0,0,3);
				self.destroy();
				}
				// add here blood splat as if on monster
			// else if (	hittex.IndexOf("SK_") >= 0		|| hittex.IndexOf("SKIN2") >= 0		|| hittex.IndexOf("SKINCUT") >= 0
			// 	||		hittex.IndexOf("SKINEDGE") >= 0	|| hittex.IndexOf("SKINFACE") >= 0	|| hittex.IndexOf("SFLR") >= 0
			// 	||		hittex.IndexOf("SKIN") >= 0		|| hittex.IndexOf("ZFACE") >= 0	    
			// 	||		hittex.IndexOf("SPINE") >= 0	|| hittex.IndexOf("SNAKE") >= 0		|| hittex.IndexOf("SLOPPY") >= 0
			// 	||		hittex.IndexOf("SP_FACE") >= 0
			// 			) {
			// 	decaltype = "Bulletchip";
			// 	dtype = "bloody";
			// 	debris = true;
			// 	smoke = 0;
			// 	A_Playsound("bullet/body",0,1.0,0,3);				
			// 	}
			else if (puffdata.HitType != TRACE_HitNone) { //BROWN GRAY CEMENT and the rest, stone seems like a good generic type
				decaltype = "Bulletchip";
				dtype = "stone";
				rchance = 25;
				debris = true;
				smoke = 2;
				A_Playsound("bullet/stone",0,1.0,0,3);				
				}
			else {
				decaltype = "BulletMchip";
				rchance = 40;
				sparkchance = 50;
				debris = false;
				smoke = 0;
				A_Playsound("bullet/metal",0,1.0,0,3);				
				}
			if (fdoom_debris == 1) {
				if (random(0,100) < rchance)
					A_SpawnProjectile("RicochetBullet",0,0,random(0,359),CMF_AIMDIRECTION,random(-45,45));
				if (smoke == 1) {
					for (int i = 0; i <= 10; i++) {
						double ws = frandom(0.1,1.2);
						A_SpawnItemEx("RicochetBullet", 0,0,0, cos(-pitch)*ws, 0, sin(pitch)*ws, random(175,185),0,				48);
						}
					}
				else if (smoke == 2) {
					if (pos.z > floorz+8 && pos.z < ceilingz-8) {
						for (int i = 0; i <= 15; i++)

							A_SpawnItemEx("RicochetBullet",	0,0,0,	frandom(-0.7,0.7),frandom(-0.7,0.7),frandom(-0.7,0.7),	0, 0 ,				64);
						}
					else {
						for (int i = 0; i <= 15; i++)
							A_SpawnItemEx("RicochetBullet",	0,0,0, frandom(-0.7,0.7),frandom(-0.7,0.7),0, 0,	SXF_ABSOLUTEVELOCITY,				64);
						}
					}
				if (debris) {
					for (int i = 0; i <= 5; i++)
						A_SpawnItemEx("RicochetBullet", 0,0,0,	frandom(2,5), 0, frandom(1,4), random(150,210),SXF_SETTARGET,	64);
					}
				if (random(0,100) < sparkchance) {
					for (int i = 0; i <= 6; i++)
						A_SpawnItemEx("RicochetBullet",	0,0,0,	frandom(-1.5,1.5),	frandom(-1.5,1.5),	frandom(1,4),		0,				0,				72);
					}
				}
			if (pos.z > floorz+8 && pos.z < ceilingz-8)
				A_SprayDecal(decaltype,8);
			//Console.PrintF("z %d, wallangle %d, %s - %s",traceheight,hitangle,hittex,dtype);
			}
		// BPUF ABCD 1 bright;
		stop;
	}
}


class RicoChet : Actor// abstract
{
	Default
	{
	-COUNTKILL
	-SHOOTABLE
	+CLIENTSIDEONLY
	+BLOODLESSIMPACT 
	+FORCEXYBILLBOARD
	+THRUACTORS
	+NOGRAVITY
	renderstyle "Add";
	alpha 1.0;
	Scale 0.09;
	Speed 0;
	Radius 1;
	Height 1;
	}

  states
  {
  Spawn:
    TNT1 A 0;
    TNT1 A 0 A_CheckFloor("SpawnFloor");
	TNT1 A 0 A_SpawnItemEx ("DetectFloorBullet",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
	TNT1 A 0 A_SpawnItemEx ("DetectCeilBullet",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
	TNT1 AAAAAAAAAA 0 A_SpawnProjectile ("SparkXNoModel", 2, 0, random (0, 360), 2, random (0, 360));
	PUFF A 0 bright A_PlaySound("ricochet/hit");

	TNT1 A 0 A_Jump(255, "Spawn1", "Spawn2", "Spawn3", "Spawn4");
    
	Spawn1:
	IPF2 AABB 1 bright A_SpawnItem ("YellowFlareSmall", 0);
    Stop;
	
	Spawn2:
	IPF2 CCDD 1 bright A_SpawnItem ("YellowFlareSmall", 0);
    Stop;
	
	Spawn3:
	IPF2 EEFF 1 bright A_SpawnItem ("YellowFlareSmall", 0);
    Stop;
	
	Spawn4:
	IPF2 GGHH 1 bright A_SpawnItem ("YellowFlareSmall", 0);
    Stop;
	
	SpawnFloor:
	TNT1 A 0;
	TNT1 A 2;

	TNT1 A 0 A_SpawnItemEx ("DetectFloorBullet",0,0,5,0,0,0,0,SXF_NOCHECKPOSITION,0);
	TNT1 A 0 A_SpawnItemEx ("DetectCeilBullet",0,0,0,-5,0,0,0,SXF_NOCHECKPOSITION,0);

	TNT1 AAAAAAAAAA 0 A_SpawnProjectile ("SparkXNoModel", 2, 0, random (0, 360), 2, random (30, 160));
	PUFF A 0 bright A_PlaySound("ricochet/hit");
	IPF2 GGHH 1 bright A_SpawnItem ("YellowFlareSmall", 0);
    stop;

  }
}